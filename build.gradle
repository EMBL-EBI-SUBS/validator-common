buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.sourcemuse.mongo' version '1.0.0'
}

group 'uk.ac.ebi.subs'
version '3.14.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    api 'uk.ac.ebi.subs:subs-data-model:2.15.0-SNAPSHOT'

    compileOnly("org.projectlombok:lombok:1.18.10")
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb:1.5.21.RELEASE'
    api 'org.springframework.boot:spring-boot-starter-data-rest:1.5.21.RELEASE'

    testImplementation 'org.springframework.boot:spring-boot-starter-test:1.5.21.RELEASE'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
}

task jarWithAddedManifest(type: Jar) {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Source-Compatibility': project.sourceCompatibility)
    }
    with jar
}

task sourceJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'validator-common'

            pom {
                name = 'validator-common'
                description = 'Data Submission Portal - common library for validation'
                url = 'https://github.com/EMBL-EBI-SUBS/validator-common'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'subs-internal'
                        name = 'EMBL-EBI-SUBS'
                        email = 'subs-internal@ebi.ac.uk'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/EMBL-EBI-SUBS/validator-common.git'
                    developerConnection = 'scm:git:ssh://github.com/EMBL-EBI-SUBS/validator-common.git'
                    url = 'https://github.com/EMBL-EBI-SUBS/validator-common.git'
                }
            }

            from components.java
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

test {
    runWithMongoDb = true
}

mongo {
    mongoVersion = "3.4.3"
}
